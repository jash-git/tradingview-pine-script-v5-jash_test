//@version=5
//tradingview V5程式 開發 規則如下
//EMA3和13 金/死叉 和 MACD 金/死叉 兩者不超過三根K棒的的間隔 就顯示進場訊號
//EMA3和34 金/死叉 顯示平倉訊號
indicator("EMA + MACD Golden Cross Entry Signal", overlay=true)

RunEMA144 = input.bool(true, "讓判斷機制參考EMA144", tooltip="勾選讓判斷機制參考EMA144")

// 計算EMA
ema3 = ta.ema(close, 3)
ema13 = ta.ema(close, 13)
ema34 = ta.ema(close, 34)
ema55 = ta.ema(close, 55)
ema144 = ta.ema(close, 144)

//---
//繪製EMA線
plot(ema3, color=color.red, linewidth=2,title="ema3")
plot(ema13, color=color.orange, linewidth=2,title="ema13")
plot(ema34, color=color.blue, linewidth=2,title="ema34")
plot(ema55, color=color.purple, linewidth=2,title="ema55")
plot(ema144, color=color.black, linewidth=2,title="ema144")
//---繪製EMA線

// ATR 計算+停損點計算
atrLength = input.int(13, minval=1)
atrValue = ta.sma(ta.tr, atrLength)

StopLossPoint=""
if atrValue[0] > (high[0] - low[0])
    StopLossPoint := "停損點數: " + str.tostring(atrValue[0]*1.5,"0.00000")
else if atrValue[0]*2 >= (high[0] - low[0])
    StopLossPoint := "停損點數: " + str.tostring(atrValue[0]*2,"0.00000")
else if atrValue[0]*3 >= (high[0] - low[0])
    StopLossPoint := "停損點數: " + str.tostring(atrValue[0]*3,"0.00000")
else
	StopLossPoint := "停損大於3倍ATR"
	
// 當前與前一根 K 棒振幅
range_curr = high - low
range_prev = high[1] - low[1]

// 計算差值的絕對值
if ((range_curr > range_prev) and ((range_curr / range_prev) > 3))
	StopLossPoint := "前後K棒大小超過3倍"
if ((range_curr <= range_prev) and ((range_prev / range_curr ) > 3))
	StopLossPoint := "前後K棒大小超過3倍"	
	
// 計算MACD
[macdLine, signalLine, _] = ta.macd(close, 3, 15, 6)

// EMA 金/死叉定義
EMA_GoldenCross = ta.crossover(ema3, ema13) and (close>=ema3) and (ema13>ema34) and (ema34>ema55) and ((ema55>ema144) or (not RunEMA144))
EMA_GClosePosition = ta.crossunder(ema3[0], ema34[0]) and (low<=ema34) and (ema3>=ema144) and (ema34>=ema144)
EMA_DeathCross = ta.crossunder(ema3, ema13) and (close<=ema3) and (ema13<ema34) and (ema34<ema55) and ((ema55<ema144) or (not RunEMA144))
EMA_DClosePosition = ta.crossover(ema3[0], ema34[0]) and (high>=ema34)  and (ema3<=ema144) and (ema34<=ema144)

// MACD 金[零軸之上]/死[零軸之下]叉定義
nearZero    = math.abs(macdLine) < 0.00001  // 定義「接近零軸」
MACD_GoldenCross = ta.crossover(macdLine, signalLine) and (nearZero or (macdLine>0))
MACD_DeathCross = ta.crossunder(macdLine, signalLine) and (nearZero or (macdLine<0))

// 記錄最近3根K棒內是否發生過金/死叉
var int EMA_GoldenCross_bar_index = na
var int MACD_GoldenCross_bar_index = na
var int EMA_DeathCross_bar_index = na
var int MACD_DeathCross_bar_index = na

if (EMA_GoldenCross)
    EMA_GoldenCross_bar_index := bar_index
if (MACD_GoldenCross)
    MACD_GoldenCross_bar_index := bar_index
if (EMA_DeathCross)
    EMA_DeathCross_bar_index := bar_index
if (MACD_DeathCross)
    MACD_DeathCross_bar_index := bar_index	

// 判斷是否同時在3根K棒內發生金/死叉
Buy_start = not na(EMA_GoldenCross_bar_index) and not na(MACD_GoldenCross_bar_index) and math.abs(EMA_GoldenCross_bar_index - MACD_GoldenCross_bar_index) <= 2
Sell_start = not na(EMA_DeathCross_bar_index) and not na(MACD_DeathCross_bar_index) and math.abs(EMA_DeathCross_bar_index - MACD_DeathCross_bar_index) <= 2

if(Buy_start)
    EMA_GoldenCross_bar_index := na
    MACD_GoldenCross_bar_index := na
	
if(Sell_start)
    EMA_DeathCross_bar_index := na
    MACD_DeathCross_bar_index := na	
    
// 畫出進場訊號
Sell_text="開倉_賣"+StopLossPoint
plotshape(Buy_start, location=location.belowbar, color=color.red, style=shape.triangleup, text="開倉_買",textcolor=color.red,title="開倉_買")
if Buy_start
    label.new(bar_index, (low - atrValue), text=StopLossPoint, style=label.style_label_up, color=color.red)
plotshape(Sell_start, location=location.abovebar, color=color.green, style=shape.triangledown, text="開倉_賣",textcolor=color.green,title="開倉_賣")
if Sell_start
    label.new(bar_index, (high + atrValue), text=StopLossPoint, style=label.style_label_down, color=color.green)

plotshape(EMA_GClosePosition, location=location.belowbar, color=color.black, style=shape.triangleup, text="平倉_賣",textcolor=color.black,title="平倉_賣")
plotshape(EMA_DClosePosition, location=location.abovebar, color=color.black, style=shape.triangledown, text="平倉_買",textcolor=color.black,title="平倉_買")